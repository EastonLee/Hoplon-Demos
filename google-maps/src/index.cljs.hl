(page "index.html"
  (:refer-hoplon :exclude [center])
  (:require [tailrecursion.google.api :as g :refer [google-map]]))

(reset! g/api-key "AIzaSyA3SuTQQlfhLL0O--JS91QqUkKBJp6HE7g")

(defn do-steps [msec [step & steps]]
  (with-timeout msec
    (when step (step))
    (when steps (do-steps msec steps))))

(defelem info [{:keys [title body]} _]
  (div :css {:height "100px" :width "200px"}
    (h3 title)
    (p body)))

(defn bounce-marker [marker]
  (.setAnimation marker (.. js/google -maps -Animation -BOUNCE))
  (with-timeout 1400 (.setAnimation marker nil)))

(defc ready? false)
(defc center {:lat -34.397 :lon 150.644})
(defc opts   {:zoom 8})
(defc pins   [{:lat  -34.197
               :lon  150.644
               :info (info :title "Foo" :body "This is a good place.")}
              {:lat  -34.397
               :lon  150.644
               :info (info :title "Bar" :body "This is also a good place.")}
              {:lat  -34.397
               :lon  150.544
               :info (info :title "Baz" :body "This is the best place.")}])

(with-init!
  (with-timeout 4000 (reset! ready? true))
  (with-timeout 2000
    (do-steps 500
      [#(swap! pins update-in [0 :lon] (partial + 0.1))
       #(swap! pins update-in [1 :lon] (partial + 0.1))
       #(swap! pins update-in [2 :lon] (partial + 0.1))
       #(swap! pins pop)
       #(swap! pins conj {:lat  -34.397
                          :lon  150.544
                          :info (info :title "Baz" :body "The best place, back again!")})
       #(swap! center update-in [:lon] (partial + 0.2))
       #(swap! center update-in [:lat] (partial + 0.2))
       #(swap! opts update-in [:zoom] inc)
       #(swap! opts update-in [:zoom] dec)
       (constantly true)
       (constantly true)
       #(swap! pins conj {:lat  -33.000
                          :lon  151.000
                          :info (info :title "Baf" :body "The a place that didn't fit in the map originally.")})
       (constantly true)
       (constantly true)
       #(reset! pins
          [{:lat  (+  39.00 (* 10 (- (rand) 0.5)))
            :lon  (+ -77.00 (*  1 (- (rand) 0.5)))
            :info (info :title "Random 1" :body "The first random point.")}
           {:lat  (+  39.00 (* 10 (- (rand) 0.5)))
            :lon  (+ -77.00 (*  1 (- (rand) 0.5)))
            :info (info :title "Random 2" :body "The second random point.")}
           {:lat  (+  39.00 (* 10 (- (rand) 0.5)))
            :lon  (+ -77.00 (*  1 (- (rand) 0.5)))
            :info (info :title "Random 3" :body "The third random point.")}])])))

(html
  (head
    (title "Hoplon â€¢ Google Maps"))
  (body :css {:text-align "center"}
    (h1 "Hoplon & Google Maps")
    (google-map
      :css       {:height "400px"
                  :width  "800px"
                  :margin "0 auto"
                  :border "1px solid red"}
      :center    center
      :pins      pins
      :pin-click #(bounce-marker %2)
      :map-click #(print :map-clicked %)
      :fit-pins  true
      :opts      opts)
    (google-map
      :center    {:lat -34.397 :lon 150.644}
      :ready     ready?
      :pins      @pins
      :opts      {:zoom 8}
      (div
        :css       {:height "400px"
                    :width  "800px"
                    :margin "0 auto"
                    :border "1px solid blue"}
        "Waiting for " (code "ready?") "..."))
    (p (a :href "https://github.com/tailrecursion/hoplon-demos/blob/master/google-maps/src/index.cljs.hl" "source code"))))
