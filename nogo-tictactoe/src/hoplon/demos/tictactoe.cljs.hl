(ns hoplon.demos.tictactoe)

(def transpose (partial apply map vector))
(def diagonal  (partial map (comp first drop) (range)))
(def indexed   (partial map-indexed vector))
(def new-game  #(vec (repeat % nil)))

(defelem scoreboard
  [{:keys [history]} _]
  (let [sorted (cell= (indexed (->> history (sort-by second) reverse)))]
    (table :class "score"
      (tr (th "rank") (th "player") (th "score"))
      (loop-tpl :bindings [[rank [player score]] sorted]
        (tr
          (td :align "center" (text "~(inc rank)"))
          (td :align "center" (text "~{player}"))
          (td :align "center" (text "~{score}")))))))

(defelem game
  [{:keys [size history] :or {size 3 history (cell [])} :as attr} _]
  (let [rowsize   (js/parseInt size)
        sizen     (* rowsize rowsize)
        game      (cell  (new-game sizen))
        rows      (cell= (partition rowsize game))
        columns   (cell= (transpose rows))
        diagonals (cell= [(diagonal rows) (diagonal (map reverse rows))])
        runs      (cell= (concat rows columns diagonals))
        winner    (cell= (->> runs
                              (map set)
                              (remove #(contains? % nil))
                              (filter #(= 1 (count %)))
                              ffirst))
        moves     (cell= (->> (indexed game)
                              (filter (comp nil? second))
                              (map first)))
        no-moves? (cell= (not (seq moves)))
        over      (cell= (cond winner    (str winner " won!")
                               no-moves? "Cat's game."))
        ai!       (fn []
                    (when-not @over
                      (swap! game assoc (rand-nth @moves) "O")))
        play!     (fn [i j]
                    (let [idx (+ (* i rowsize) j)]
                      (when (and (not @over)
                                 (nil? (get @game idx)))
                        (swap! game assoc idx "X"))))]
    (reset! history {"X" 0 "O" 0 "cat" 0})
    (cell=
      (when (or winner no-moves?)
        (swap! ~(cell history) update-in [(or winner "cat")] inc)))
    (div (dissoc attr :size :history)
      (table :class "tictac"
        (loop-tpl :bindings [[i row] (cell= (indexed rows))]
          (tr
            (loop-tpl :bindings [[j x] (cell= (indexed row))]
              (td :click #(and (play! @i @j) (ai!)) (text "~{x}"))))))
     (div :toggle over
       (p (text "~{over}"))
       (button :click #(reset! game (new-game sizen)) "Play Again")))))
