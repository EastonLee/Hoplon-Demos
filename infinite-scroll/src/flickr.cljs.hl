(ns flickr)

(defn image-search
  "Given a search query and a callback function, a JSONP request to the Flickr
  API is made. For each search result the link to the flickr page and the URL
  of the image are returned as {:link <link URL>, :image <image URL>}."
  [query success fail]
  (let [js->clj #(js->clj % :keywordize-keys true)
        data    {:tags query :tagmode :any :format :json}
        url     "http://api.flickr.com/services/feeds/photos_public.gne?jsoncallback=?"
        ajax    (clj->js {:url url :dataType "jsonp" :timeout 2000 :data data})
        parse   (fn [{link :link, {m :m} :media}] {:link link :image m})]
    (doto (.ajax js/jQuery ajax)
      (.fail #(apply fail %&))
      (.done #(->> % js->clj :items (map parse) success)))))
