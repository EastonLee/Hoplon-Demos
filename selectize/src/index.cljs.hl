(page "index.html"
  (:require
   [selectize :as s]
   [tailrecursion.hoplon.reload :refer [reload-all]]))

(reload-all)

(defn rotten-tomatoes [query callback]
  (.ajax js/jQuery
         (clj->js
          {:url "http://api.rottentomatoes.com/api/public/v1.0/movies.json"
           :type "GET"
           :dataType "jsonp"
           :data {:q query :page_limit 10 :apikey "jv2szsy84jnsbj8cdtxt866m"}
           :error #(callback)
           :success #(callback (aget % "movies"))})))

(defn movie-option [movie]
  (let [{:strs [title synopsis abridged_cast]} movie]
    (div
      (img :src (get-in movie ["posters" "thumbnail"]))
      (span :class "title" (span :class "name" title))
      (span :class "description"
        (if (empty? synopsis) "No synopsis available at this time." synopsis))
      (span :class "actors"
        (if (seq abridged_cast)
          (->> abridged_cast
               (map (comp span #(get % "name")))
               (interpose ", ")
               (cons "Starring "))
          "Actors unavailable.")))))

(html
  (head
    (title "Hoplon Selectize")
    (link :rel "stylesheet" :type "text/css" :href "selectize.css")
    (link :rel "stylesheet" :type "text/css" :href "selectize_movies.css"))
  (body
    :style "font-family: sans-serif; width: 40%; margin: auto;"
    (h3 "Rotten Tomatoes Movie Title Search")
    (let [movies   (cell {})
          movie-id (cell nil)
          movie    (cell= (get movies movie-id))
          poster   (cell= (get-in movie ["posters" "detailed"]))
          title    (cell= (get movie "title"))
          year     (cell= (get movie "year"))
          starring (cell= (map #(get % "name") (get movie "abridged_cast")))]
      (div
        (s/selectize
         ;; hoplon-specific options
         :cell        movie-id
         :class       "movies"
         ;; selectize options
         :valueField  "id"
         :labelField  "title"
         :searchField "title"
         :create      false
         :render      {:option #(do (swap! movies assoc (get % "id") %) (movie-option %))}
         :load        rotten-tomatoes)
        (div :toggle movie
          (img :style "float:right;" :src poster)
          (h2 (text "~{title} (~{year})"))
          (h2 "Starring")
          (ul
            (loop-tpl :bindings [star starring]
              (li (text "~{star}")))))
        (div :style "text-align:center;"
          (a :href "https://github.com/tailrecursion/hoplon-demos/blob/master/selectize/src/index.cljs.hl"
      "Hoplon Source Code"))))))
