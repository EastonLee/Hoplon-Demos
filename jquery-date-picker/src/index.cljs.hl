(page "index.html"
  (:refer-hoplon :exclude [center])
  (:require
   [tailrecursion.hoplon.reload :refer [reload-all]]
   [hoplon.jquery.daterangepicker :as d :refer [daterange]]
   [hoplon.twitter.bootstrap
    :refer [checkbox container control-label form-group form-horizontal]]))

(reload-all)

(defc close? true)
(defc dates1 nil)
(defc dates2 [(js/Date.) (js/Date.)])

(cell= (print :close? close?))
(cell= (print :dates1 dates1))
(cell= (print :dates2 dates2))

(defelem feedback-remove
  [attr _]
  ((span :class "glyphicon glyphicon-remove form-control-feedback") attr))

(defelem date-picker-group
  [{:keys [label state opts] :as attr} _]
  ((form-group :class "has-feedback"
     (control-label :col {:sm 2} :text label)
     (div :col {:sm 10}
       (daterange
         :opts        opts
         :state       state
         :class       "form-control"
         :placeholder "Enter a date range")
       (feedback-remove
         :css    {:cursor "pointer"}
         :toggle state
         :click  #(reset! state nil))))
   (dissoc attr :label :state :opts)))

(defelem checkbox-group
  [{:keys [state label] :as attr} _]
  ((form-group
      (div :col {:sm 10} :offset {:sm 2}
        (checkbox
          (tailrecursion.hoplon/label
            (input
              :type "checkbox"
              :value state
              :change #(do (swap! state not) false))
            (text "~{label}")))))
   (dissoc attr :state :label)))

(defelem button-group
  [{:keys [label click] :as attr} _]
  (form-group
    (div :col {:sm 10} :offset {:sm 2}
      (button
        :type  "button"
        :class "btn btn-default"
        :click click
        :text  label))))

(html :lang "en"
  (head
    (title "Hoplon â€¢ jQuery Date Range Picker"))
  (body
    (container
      (h1 "Hoplon & jQuery Date Range Picker")
      (hr)
      (form-horizontal
        (date-picker-group
          :label   "Date Range 1"
          :state   dates1
          :success dates1
          :opts    (cell= {:autoClose close?}))
        (date-picker-group
          :label   "Date Range 2"
          :state   dates2
          :success dates2
          :opts    (cell= {:autoClose close?}))
        (checkbox-group
          :state close?
          :label "auto-close enabled")
        (button-group
          :label "clear"
          :click #(do (reset! dates1 nil) (reset! dates2 nil))))
      (hr)
      (p :css {:text-align "center"}
        (a :href "https://github.com/tailrecursion/hoplon-demos/blob/master/jquery-date-picker/src/index.cljs.hl" "source code")))))
